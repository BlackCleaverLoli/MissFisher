# 钓法表达式（Angle Expression）
> 本文默认读者已经掌握《最终幻想14》中的基本操作和钓鱼玩法的各种基本要点。

# 本文还未完工

## 什么是钓法表达式？
钓法表达式是一段用来描述 FF14 钓鱼玩法策略的文本，它由极简的关键字和符号组成，能快速表达特定场景或目标下的钓鱼方法。

> 对于 MissFisher 来说，钓法表达式是用来快速为 MissFisher 输入钓鱼策略参数的。

# 0. 开始

## 0.1 怎么用？
在已经启用 MissFisher 时，在游戏的聊天窗口（消息窗口）输入这样的命令：
```  
/mf <angex>  
```  
例如钓法表达式是 `平钓》！！！`，那么：
```  
/mf 平钓》！！！  
```  

又或者，你可以在 MissFisher 的[钓法表达式编辑器](#02-钓法表达式编辑器)中启动。


## 0.2 钓法表达式编辑器
在 MissFisher 主界面选择「预设」页签，点击「切换」，就能从钓法表达式生成器切换到钓法表达式编辑器。

钓法表达式编辑器可以：

- 即时校验钓法表达式是否合法
- 将单行的钓法表达式按结构添加换行和缩进使其在编辑时更有可读性


# 1. 基本结构
> 模式关键字 》 阶段参数

一条最简的钓法表达式由「模式关键字」「阶段起始符」和「阶段参数」组成。

- 例如：
```angex  
平钓》！  
nm > !  
```  

**表达钓法：**  
抛竿、对 `!` 提钩。


# 2. 模式
## 2.1 平钓
> 普通的抛竿

`平钓` / `nm` 表示在这段钓法中不需要任何提升鱼类大尺寸概率的技能。即只需要抛竿，目标鱼类就有可能咬饵。

例如，钓场「红茶川水系下游」的钓场之王[「酿血鱼」](https://fish.ffmomola.com/#/wiki/fishing/spot/91/fish/7696)（ID 7696），它的钓法在各种攻略/工具中常被这样表示：

> 「蝲蛄丸子」→ `!!!`「酿血鱼」

简而言之：使用「蝲蛄丸子」，抛竿后对 `!!!` 提钩。

- 先写 `平钓` / `nm`
    - 表示在钓法中不使用提高大尺寸概率的技能，例如大鱼猎手或耐心系技能。
```angex  
平钓  
nm  
```  

- 然后使用 `》` / `>` 引入阶段参数
    - 每个「阶段」都必须使用 `》` / `>` 来引入参数
    - 通常来说，每次「从头开始」的抛竿和以小钓大，都算作一个单独的阶段
    - 参数之间的空格不是必须的
```angex  
平钓》  
nm >  
```  

- 最后，加上表示需要提钩的咬饵的类型符号
    - 在这里，我们使用 3 个 `！` / `!` 来表示对应「酿血鱼」的咬饵类型：`！！！` / `!!!`
```angex  
平钓》！！！  
nm > !!!  
```  
**表达钓法：**  
直接抛竿、对 `!!!` 提钩。

> **- 助记：**  
> `平钓` / `nm` = 普通的抛竿

## 2.2 大鱼
> 大鱼猎手

`大鱼` / `bf` 表示在钓法中需要使用大鱼猎手，来提高获得大尺寸鱼类的概率。

**- 通常而言，我们在以下场景使用 `大鱼` / `bf`：**  
目标鱼类 / 用来以小钓大的鱼类 需要大尺寸，咬饵频次低且特征好分辨。

例如，钓场「彻悟岩窟」的钓场之王[「巨陨龟」](https://fish.ffmomola.com/#/wiki/fishing/spot/121/fish/15631)（ID 15631），它的钓法在各种攻略/工具中常被这样表示：

> 「金属亮片」→ `!`「花岗蟹」→ `!!!`「巨陨龟」

简而言之：使用「金属亮片」，抛竿后对 `!` 提钩，然后用「花岗蟹」以小钓大，对 `!!!` 提钩。  
也就是说，「巨陨龟」只需要 1 次以小钓大。

同时，在「彻悟岩窟」使用「金属亮片」时，咬饵的 `!` 必定是「花岗蟹」，咬饵的 `!!` 必定是「熔岩鲎」。  
这意味着，用来以小钓大的鱼类的咬饵特征非常好分辨。

只需要 1 次以小钓大，用来以小钓大的鱼类的咬饵特征又非常好分辨，这正好符合之前提到的 `大鱼` / `bf` 的使用场景。

- 先写钓「花岗蟹」的表达式，使用 `大鱼` / `bf`、然后是 `》` / `>`，补上咬饵类型 `!`
```angex  
大鱼》！  
bf > !  
```  

- 再接上第 2 个 `》` / `>`，表示在抛竿-提钩阶段结束后，尽可能使用以小钓大
```angex  
大鱼》！》  
bf > ! >  
```  

- 最后写上以小钓大后，需要提钩的目标鱼类的咬饵类型，即对应「巨陨龟」的 3 个 `！` / `!`
```angex  
大鱼》！》！！！   
   
bf > ! > !!!  
```  
**表达钓法：**  
用大鱼猎手；  
在抛竿后对 `!` 提钩；尽可能以小钓大；在以小钓大后对 `!!!` 提钩。

> **- 助记：**  
> `大鱼` / `bf` = 用「大鱼猎手」的钓法

## 2.3 耐心
> 耐心系技能：耐心I、耐心II

`耐心` / `pt` 表示在钓法中需要使用「耐心系技能」（如耐心II），以在高频提钩场景下高效地提高获得大尺寸鱼类的概率。

**- 通常而言，我们在以下场景使用 `耐心` / `pt`：**  
目标鱼类 / 用来以小钓大的鱼类 需要大尺寸，咬饵频次高或特征难以分辨；  
或是目标鱼类需要使用 2 次或以上次数的以小钓大才能钓到。

例如，钓场「调查队冰洞」的钓场之王「一家之母」（ID 7946），它的钓法在各种攻略/工具中常被这样表示：

> 「残暴水蛭」→ `!`「阿巴拉提亚公鱼」→ `!!!`「一家之母」

简而言之：使用「残暴水蛭」，抛竿后对 `!` 提钩，然后使用「阿巴拉提亚公鱼」以小钓大，对 `!!!` 提钩。

在「调查队冰洞」中使用「残暴水蛭」垂钓时，咬饵的必定是「阿巴拉提亚公鱼」，如果仍使用技能大鱼猎手来提升大尺寸概率，采集力会在频繁的咬饵下迅速亏空。

因此，对于这种场景，我们会在钓法中使用耐心II而不是大鱼猎手：

- 先写钓「阿巴拉提亚公鱼」的表达式，使用 `耐心` / `pt`
```angex  
耐心》！  
pt > !  
```  

- 然后补全以小钓大后的阶段参数
```angex  
耐心》！》！！！  
pt > ! > !!!  
```  
**表达钓法：**  
保持耐心系技能的状态效果；  
在抛竿后对 `!` 提钩；尽可能以小钓大；在以小钓大后对 `!!!` 提钩。

> **- 助记：**  
> `耐心` / `pt` = 用「耐心II」的钓法

# 3. 咬饵类型
> 竿型 杆型
## 3.1 多个咬饵类型

`+` 连接多个咬饵类型来组成咬饵类型集合。

例如：
```angex  
平钓》！+！！！  
nm > ! + !!!  
```  
**表达钓法：**  
抛竿，对 `!` 或 `!!!` 提钩。

> - **约定：**  
    在连接多个咬饵类型时，应按符号数量从小到大排列  
    如：使用 `! + !!!` 而不是 `!!! + !`

## 3.2 全部
`全部` / `all` 表示「所有」咬饵类型。

`全部` / `all`  
等效于：  
`！+！！+！！！` / `! + !! + !!!`

例如：
```angex  
平钓》全部  
nm > all  
```  
**表达钓法：**  
抛竿，对 `!` 或 `!!` 或 `!!!` 提钩。

## 3.3 额外咬饵类型
在 `》` / `>` 后、被 `（ ）` / `( )` 包裹的是额外咬饵类型。表示需要对符合额外咬饵类型的咬饵使用「提钩」。

- 例如：
```angex  
平钓》（！！！）~11+20！！双提  
nm > (!!!) ~ 11 + 20 !! dh  
```  
**表达钓法：**  
抛竿，对 20 秒以下（撒饵时 11 秒以下）的 `!!` 尽可能使用「双重提钩」；  
超时不歇竿，对 `!!!` 使用「提钩」。

> MissFisher 不会在当前阶段存在额外咬饵类型时、因为等待时间超过指定的最大咬饵时间而使用歇竿。

## 3.4 以小钓大循环
> 套娃

游戏中有这样的场景，当钓上鱼类 A，用它以小钓大并提钩后，钓上来的仍可能是鱼类 A，这被称为「以小钓大循环」。

例如钓场「深壕」的钓场之皇「潜龙」，必须用「以太螯虾」以小钓大才能钓到它。而用「以太螯虾」以小钓大，也可能钓到「以太螯虾」，这在各种攻略、钓鱼工具上可能会这样表示：

> 「蜉蝣」→ `!!`「以太螯虾」（循环）→ `!!!`「潜龙」

- 只需要把相同的咬饵类型加入以小钓大后的咬饵类型集合中，就能表达这种以小钓大循环：
```angex  
耐心》！！》！！+！！！  
pt > !! > !! + !!!  
```  
**表达钓法：**   
保持耐心系技能的状态效果；  
抛竿，对 `!!` 使用提钩；  
尽可能使用以小钓大，然后对 `!!` 或 `!!!` 使用提钩。

# 4. 咬饵时间
## 4.1 不撒饵时的咬饵时间
咬饵类型前被 `~` / `-` 连接的数字表示的是目标在未撒饵时的最短与最长咬饵时间。

- 接受的咬饵时间的单位是秒
- 最多支持 1 位小数

- 例如：
```angex  
平钓》10~32！！！  
nm > 10 ~ 32 !!!  
```  
**表达钓法：**  
直接抛竿、对咬饵时间在 10 秒以上、32 秒以下的 `!!!` 提钩。

> MissFisher 会在等待时间超过指定的咬饵时间时使用歇竿来取消等待。

- 可省略最短 / 最长咬饵时间以表示忽略最长 / 最短咬饵时间的限制
    - 省略可以让表达式更简洁
- 例如：
```angex  
平钓》10~！！！  
nm > 10 ~ !!!  
```  
**表达钓法：**  
直接抛竿、对咬饵时间在 10 秒以上的 `!!!` 提钩。

## 4.2 撒饵时的咬饵时间
`+` 连接的数字表示一组目标在不同状态时的咬饵时间，`+` 前的数字表示有撒饵时的咬饵时间，而 `+` 后的则是未撒饵时的咬饵时间。

- 例如：
```angex  
平钓》6+10~17+32！！！  
nm > 6 + 10 ~ 17 + 32 !!!  
```  
**表达钓法：**  
直接抛竿；  
对咬饵时间在 10 秒以上（撒饵时 6 秒以上）、32 秒以下（撒饵时 17 秒以下）的 `!!!` 提钩。

> MissFisher 会在表达式中仅提供未撒饵时的咬饵时间时，按简单的公式补全出撒饵时的咬饵时间。  
> 例如：   
> `10 ~ 32`  
> 补全后：    
> `5 + 10 ~ 17 + 32`

# 5. 提钩类型
咬饵类型后的关键字表示在对应的咬饵出现时应使用的提钩。

## 5.1 携带耐心状态时的提钩
咬饵类型后的 `强力` / `pw` 或 `精准` / `pc` 表示在符合前方参数的咬饵出现时，如果处在耐心系技能的负面状态效果，应使用的提钩技能。

- 可以这样表达应对 `!!!` 类型的咬饵使用「精准提钩」
```angex  
耐心》！》！！！精准  
pt > ! > !!! pc  
```  
**表达钓法：**  
……在以小钓大后对 `!!!` 提钩（如果处在耐心状态、则使用精准提钩）。

> - **约定：**  
    `!!!` 后如果没有 `精准` / `pc` 则默认使用「强力提钩」

> MissFisher 会固定对 `!` 使用「精准提钩」、  
> 对 `!!` 使用「强力提钩」；  
> 对 `!!!` 默认使用「强力提钩」。

## 5.2 复数的提钩
> 多提

咬饵类型后的 `双重` / `双提` / `dh` 或 `三重` / `三提` / `th` 表示在符合前方参数的咬饵出现时，应使用的可增加捕获到的鱼的数量的提钩技能。

- 例如，表达使用「双重提钩」：
```angex  
平钓》！双重  
平钓》！双提  

pt > ! dh  
```  
**表达钓法：**  
抛竿，`!` 出现时，尽可能使用「双重提钩」。

## 5.3 其他特殊的提钩
咬饵类型后的 `华丽` / `sh` 表示在符合前方参数的咬饵出现时，应使用「华丽提钩」。

- 例如：
```angex  
平钓》！！！华丽  
pt > !!! sh  
```  
**表达钓法：**  
抛竿，`!!!` 出现时，尽可能使用「华丽提钩」。

# 6. 用来以小钓大的目标
在表示以小钓大参数起始的 `》` / `>` 前的、被 `【 】` / `[ ]` 包裹的名称或数字 ID 表示用来以小钓大的目标鱼类。

例如钓场之王「一家之母」，需要使用「阿巴拉提亚公鱼」来以小钓大才能钓到它，钓法如：

> 「残暴水蛭」→ `!`「阿巴拉提亚公鱼」→ `!!!`「一家之母」


- 显式表达应使用「阿巴拉提亚公鱼」来以小钓大：
    - 在表示以小钓大的第 2 个 `》` / `>` 开始前添加
```  
- 耐心》！》！！！  
+ 耐心》！【阿巴拉提亚公鱼】》！！！  
- pt > ! > !!!   
+ pt > ! [Abalathian Smelt] > !!!  
```  


# 7. 内联表达式

## 7.1 拍击水面
紧跟着 `@` 的 `拍水` / `ss` 开始的是拍击水面类型的内联表达式。

假设拍水目标是 `魇鱼` / `Feverfish`：


- 在平钓后的阶段参数最后写 `@`
```  
- 平钓》全部  
+ 平钓》全部@  
- nm > all  
+ nm > all @  
```  

- 加上 `拍水` / `ss`
```angex  
平钓》全部@拍水  
nm > all @ss  
```  
- 最后追加要对其使用拍击水面的目标
```angex  
平钓》全部@拍水【魇鱼】  
nm > all @ss [Feverfish]  
```  
**表达钓法：**  
直接抛竿、然后对任何咬饵提钩；  
如果钓到了  `魇鱼` / `Feverfish`，则对其使用拍击水面。

- 如果钓法包含拍击水面类型的内联表达式，同时还需要以小钓大，看起来会是这样：
```angex  
耐心》全部@拍水【魇鱼】》全部  
pt > all @ss [Feverfish] > all  
```  

> **- 助记：**  
> 内联表达式：`@` 内联类型 参数

## 目标参数的其他写法

- 可以直接使用物品的数字 ID
```angex  
平钓》全部@拍水【36468】  
nm > all @ss [36468]  
```  

- `|` 连接名称和数字 ID 时可提升可读性
    - 并列时，名称在前，数字 ID 在后
```angex  
平钓》全部@拍水【魇鱼|36468】  
nm > all @ss [Feverfish|36468]  
```  

> 当名称和数字 ID 并列，MissFisher 会优先使用数字 ID；  
> 在使用名称时，最好同时使用数字 ID 来避免名称书写或识别时的错误。

- `、` / `||` 会连接多个目标组成集合
```angex  
平钓》全部@拍水【魇鱼|36468、紫鱼|36470】  
nm > all @ss [Feverfish|36468 || Calicia|36470]  
```  
**表达钓法：**  
……如果钓到了 `36468` 或者 `36470`，则尽可能对其使用拍击水面。

- `任何` / `any` 表示任何鱼类
```angex  
平钓》全部@拍水【任何】  
nm > all @ss [any]  
```  
**表达钓法：**  
……对任何鱼类都尽可能对其使用拍击水面。

- `【 】` / `[ ]` 的开头的 `？` / `?` 会对整个集合取反
```angex  
平钓》全部@拍水【？36468】  
nm > all @ss [? 36468]  
```  
**表达钓法：**  
……如果钓到了不同于 `36468` 的鱼类，则尽可能对其使用拍击水面。

- `占位` / `ph` 表示一个空集，等效于 `？任何` / `? any`
```angex  
平钓》全部@拍水【占位】  
nm > all @ss [ph]  
```  
**表达钓法：**  
……不对任何鱼类使用拍击水面。

- 像这样，可以利用 `占位` / `ph` 和拍击水面目标参数来表达需要对额外的咬饵使用提钩（而不对任何鱼类使用拍击水面）
```angex  
平钓》~9！！@拍水~5！【占位】  
nm > ~ 9 !! @ss ~ 5 ! [ph]  
```  
**表达钓法：**  
抛竿、对 9 秒以内的 `!!` 使用提钩，  
同时也对 5 秒以内 `!` 使用提钩。


## 7.1.2 拍击水面目标的咬饵参数
在 `拍水` / `ss` 后、拍击水面目标前的，是拍击水面目标的咬饵参数。

- 例如「酿血鱼」
```angex  
平钓》！！！  
nm > !!!  
```  

占据咬饵概率的 `4936` 的咬饵类型是 `!!`，不为拍击水面目标指定咬饵的话，就无法钓到 `4936` 来使用拍击水面了。
- 像这样就可以为表达式指定拍击水面目标的咬饵参数：
```angex  
平钓》！！！@拍水！！【白妙鲤|4936】  
nm > !!! @ss !! [Maiden Carp|4936]  
```  
**表达钓法：**   
抛竿、对 `!!!` 使用提钩；  
在需要拍击水面且采集力充足时，对 `!!` 使用提钩；  
如果钓到了 `4936`，则尽可能对其使用拍击水面。

- 如果还要指定咬饵时间的话：
```angex  
平钓》！！！@拍水12~！！【白妙鲤|4936】  
nm > !!! @ss 12 ~ !! [Maiden Carp|4936]  
```  
**表达钓法：**  
……在需要拍击水面且采集力充足时，对 12 秒以上的 `!!` 使用提钩；  
如果钓到了 `4936`，则尽可能对其使用拍击水面。

## 7.2 专一垂钓

紧跟着 `@` 的 `专一` / `ic` 开始的是专一垂钓类型的内联表达式。

- 例如，钓到「黄金鳗」时，对其使用专一垂钓：
```angex  
平钓》！！@专一【黄金鳗|46188】  
nm > !! @ic [Goldentail|46188]  
```  
**表达钓法：**  
……如果钓到了 `46188`，则尽可能对其使用专一垂钓。

专一垂钓类型的内联表达式无法指定咬饵特征。

- 但可以这样指定持有专一垂钓状态效果时，对咬饵使用的提钩类型：
    - 在专一提钩目标后写要使用的提钩
```angex  
平钓》！！@专一【黄金鳗|46188】双提  
nm > !! @ic [Goldentail|46188] dh  
```  

**表达钓法：**  
……如果钓到了 `46188`，则尽可能对其使用专一垂钓；  
在持有专一垂钓状态效果时，对任何咬饵尽可能使用「双重提钩」。

- 同时存在专一垂钓和拍击水面类型的内联表达式时，应先写拍击水面类型的内联表达式，然后写专一垂钓类型的内联表达式：
```angex  
平钓》！！@拍水【亚斯拉尼弓鳍鱼|43773】@专一【黄金鳗|46188】双提  
nm > !! @ss [Yyasulani Bowfin|43773] @ic [Goldentail|46188] dh  
```  
**表达钓法：**  
……如果钓到了 `43773`，则尽可能对其使用拍击水面；  
如果钓到了 `46188`，则尽可能对其使用专一垂钓；  
在持有专一垂钓状态效果时，对任何咬饵尽可能使用「双重提钩」。

# 8. 游动饵
> 鱼篓

`《` / `<` 后跟随被 `【 】` / `[ ]` 包裹的目标，表示需要将可以小钓大的目标鱼类保留为游动饵。同时表示应采用尽可能提高钓到鱼类的大尺寸概率的手法。

- 例如：
```angex  
耐心》！《【4869】  
pt > ! < [4869]  
```  
**表达钓法：**  
保持耐心系技能的状态效果；  
抛竿、对 `!` 使用提钩；  
尽可能对 `4869` 使用「熟练妙招」（来将其保留为游动饵）。


## 8.1 指定准备时存入的游动饵
`《` / `<` 指定了在准备游动饵时应保留为游动饵的鱼类。

例如，钓场「蛇水湖」的钓场之皇[「隆卡的大水蛇？」](https://fish.ffmomola.com/#/wiki/fishing/spot/225/fish/33243)，其钓法如下：

> 「盗蟹丸」→ `!`「王冠脂鲤」→ `!!`「引螈」→ `!!!`「洛克瓦卫兵」→ `!!!`「隆卡的大水蛇？」

「隆卡的大水蛇？」需要用「洛克瓦卫兵」以小钓大才能钓到。  
然而，「洛克瓦卫兵」的窗口期与前者一致，所以在「隆卡的大水蛇？」的窗口期前准备的游动饵应是用来钓「洛克瓦卫兵」的「引螈」。

- 写成表达式的话：
    - 表达在准备游动饵时，钓到 `27491` 后就应该把它保留为游动饵（而不是继续以小钓大）
```angex  
耐心@1000-1200》！》！！《【27491】》！！！》！！！  
pt @1000 - 1200 > ! > !! < [27491] > !!! > !!!  
```  

# 9. 钓饵
> 鱼饵、渔饵

模式关键字后用 `【 】` / `[ ]` 包裹的最多 1 种明确的钓饵名，表示在这段钓法中应使用的钓饵。

- 例如，钓「酿血鱼」需要使用「蝲蛄丸子」：
```angex  
平钓【蝲蛄丸子】》！！！  
nm [Crayfish Ball] > !!!  
```  
**表达钓法：**  
使用钓饵「蝲蛄丸子」；……

也可以使用和目标参数同样的其他写法，详见[目标参数的其他写法](#目标参数的其他写法)。

# 10. 窗口期
在 `》` / `>` 前用 `@` 开始窗口期条件。

`-` 连接的 4 位数表示对应鱼类会出现的 24 小时制的艾欧泽亚时间区间。

`（ ）` / `( )` 包围的天气表示对应鱼类会出现的天气，内部的 `=》` / `=>` 连接「之前的天气」和「现在的天气」。

- 例如，对应鱼类在艾欧泽亚时间的 10:00 到 12:00 才会出现：
```angex  
平钓@1000-1200》！！！  
nm @1000 - 1200 > !!!  
```  

- 又例如，对应鱼类在当前天气是 `薄雾` / `Fog` 时才会出现：
```angex  
平钓@（薄雾）》！！！  
nm @(Fog) > !!!  
```  

- 如果要求之前的天气是 `阴云` / `Clouds`，同时现在的天气是 `薄雾` / `Fog`：
```angex  
平钓@（阴云=》薄雾）》！！！  
nm @(Clouds => Fog) > !!!  
```  

- 如果同时要求时间和天气条件，先写时间，然后写天气：
```angex  
平钓@1000-1200（阴云=》薄雾）》！！！  
nm @1000 - 1200 (Clouds => Fog) > !!!  
```  

> MissFisher 会在窗口期外按钓法所需自动预备「捕鱼人之计」、游动饵等。

# 11. 全局参数
在所有阶段参数的最后以 `=` 开始，并用 `；` 或 `;` 分割或结束的是全局参数。

## 11.1 最终目标
在 `=` 后的不带数字的用 `【 】` / `[ ]` 包裹的目标集合是最终目标，它会被 `；` / `;` 结束。

最终目标表示在钓上集合中的鱼类后就停止钓鱼。

- 例如，最终目标是 `4869`
    - 先写用来钓 `4869` 的表达式：
```angex  
平钓》！  
nm > !  
```  

- 用 `=` 开始全局参数
```angex  
平钓》！=  
nm > ! =  
```  

- 接着写带 `4869` 的目标集合
```angex  
平钓》！=【4869】  
nm > ! = [4869]  
```  

- 最后用 `；` / `;` 结束
```angex  
平钓》！=【4869】；  
nm > ! = [4869];  
```  
**表达钓法：**  
……钓到 `4869` 时停止钓鱼。

## 11.2 计数器
在 `=` 后的、带数字的、用 `【 】` / `[ ]` 包裹的目标集合是计数器，它会被 `；` / `;` 结束 / 分割。

在钓到符合集合中目标的鱼类时，计数加 1。如果所有计数器已计算的数量都满足指定数量，则停止钓鱼。计数目标最多支持 3 组。

- 例如，表达钓 3 条 `4869` 就停止钓鱼：
```angex  
平钓》！=3【4869】；  
nm > ! = 3 [4869];  
```  

- 又或者，表达钓 3 条 `4869` 和 2 条 `4904` 就停止钓鱼：
```angex  
平钓》！=3【4869】；2【4904】；  
nm > ! = 3 [4869]; 2 [4904];  
```  

- 又如，结合最终目标，钓到 `4913` 就停止钓鱼； 同时，如果钓够 3 条 `4869` 和 2 条 `4904` 也停止钓鱼：
```angex  
平钓》！=【4913】；3【4869】；2【4904】；  
nm > ! = [4913]; 3 [4869]; 2 [4904];  
```  

## 11.3 钓法修饰词
在 `=` 后的关键字是钓法修饰词，它会被 `、` / `||` 分割，并被 `；` / `;` 结束。

- 语法如：
```  
- 平钓》！  
+ 平钓》！=不撒饵；  
- nm > !  
+ nm > ! = nochum;   
```  

- 同时表达多个钓法修饰词：
```angex  
平钓》！=不撒饵、钓组；  
nm > ! = nochum || snag;   
```  

- 可与其他全局参数同时存在，如最终目标和计数目标：
```angex  
平钓》！=【4869】；2【4904】；不撒饵、钓组；  
nm > ! = [4869]; 2 [4904]; nochum || snag;   
```  

所有钓法修饰词：

| 中文输入法 |   英文输入法    |  
|:-----:|:----------:|
|  鱼眼   |     fe     |  
|  攒鱼计  |     aa     |  
|  不撒饵  |   nochum   |  
| 无强心剂  |   nocord   |  
|  无恩宠  |  nofavor   |  
| 允许溢出  |  overflow  |  
|  梭哈   |   all-in   |  
|  钓组   |    snag    |  
|  收藏品  |    coll    |  
|  不收集  |   nocoll   |  
|  大尺寸  |   large    |  
| 大鱼知识  |    bfg     |  
|  鱼篓   |     sh     |  
| 鱼篓专一  |   sh-ic    |  
| 等待专一  |   waitic   |  
| 等待华丽  |  wait-sh   |  
|  套娃   | mooch-loop |  
|  引诱   |    lure    |  
|  雄心   |   a-lure   |  
|  谦逊   |   m-lure   |  
|  鱼影   |   shadow   |  
|  重随   |  re-roll   |  
|  回收   |    recy    |  
|  加钩   |     mh     |  
| 确保多提  |    mhs     |  
| 多提回退  |  mhs-back  |  
|  银星   |   silver   |  
|  跳阶段  |  skipstg   |  
| 强制预备  | force-prep |  

## 11.3.1 鱼眼
`鱼眼` / `fe` 表示应使用这样的手法：

1. 在可用的采集力不足以高质量地使用 4 次抛竿时，等待。
2. 等待时，如刚才钓到的鱼类符合钓法中需要拍击水面的鱼，则使用拍击水面。
3. 等待时，如刚才钓到的鱼类符合钓法中用来以小钓大的目标，则使用熟练妙招，并尽可能使用专一垂钓。
4. 可用的采集力足够时，使用鱼眼。
5. 在鱼眼效果内抛竿 / 以小钓大的最后一次等待 / 咬饵结束时，回到第 1 步。
6. 当前如果处在正常的窗口期内，不使用上面的手法。

## 11.3.2 攒鱼计
`攒鱼计` / `aa` 表示应使用可积攒「捕鱼人之计」的手法，即使用增加大尺寸概率的技能，且不使用「沙利亚克的恩宠」。在「捕鱼人之计」到达 10 层时停止。

- 通常在固定的表达式中使用：
````angex  
平钓【29717】》全部=攒鱼计；  
nm [29717] > all = aa;  
````  

## 11.3.3 不撒饵
`不撒饵` / `nochum` 表示不使用「撒饵」。


## 11.3.4 无强心剂
`无强心剂` / `nocord` 表示不使用任何「强心剂」。

## 11.3.5 无恩宠
`无恩宠` / `nofavor` 表示不使用「沙利亚克的恩宠」来回复采集力。

## 11.3.6 允许溢出
`允许溢出` / `overflow` 表示即使采集力恢复技能会导致溢出而浪费采集力上限，也允许使用它们。例如「强心剂」和「沙利亚克的恩宠」。

## 11.3.7 梭哈
`梭哈` / `all-in` 表示完全不保留模式所需采集力。例如 `大鱼` / `bf` 时保留的用作「大鱼猎手」的采集力；或 `耐心` / `pt` 时保留的用作「耐心II」的采集力。

## 11.3.8 钓组
`钓组` / `snag` 表示维持技能「钓组」的状态效果。

## 11.3.9 收藏品
`收藏品` / `coll` 表示维持技能「收藏品采集」的状态效果。

## 11.3.10 不收集
`不收集` / `nocoll` 表示在钓上可收为收藏品的鱼类时，不将其收为收藏品。

## 11.3.11 大尺寸
`大尺寸` / `large` 表示应采用尽可能提升鱼类的大尺寸概率的手法。例如使用「大鱼猎手」或「耐心II」。

## 11.3.12 大鱼知识
`大鱼知识` / `bfg` 表示应尽可能在合适时使用「大鱼的知识」，同时保留「大鱼的知识」所需的「捕鱼人之计」。

- 合适的时机如：在钓法中不存在拍击水面或专一垂钓目标时，否则要求持有拍击水面或专一垂钓效果。

## 11.3.13 鱼篓
`鱼篓` / `sh` 表示应采用尽可能提升鱼类的大尺寸概率并获取更多游动饵的手法。是 [8.游动饵](#8-游动饵) 的显式表达，但不等效。

## 11.3.14 鱼篓专一
`鱼篓专一` / `sh-ic` 搭配 `@专一` / `@ic` 使用。表示在钓到用来以小钓大的目标鱼类时，应尽可能使用这样的连招，以获取更多的以小钓大机会：先使用「熟练妙招」将其保留为游动饵，然后使用「专一垂钓」。

- 例如钓场之王「海牛灾星」，用来以小钓大的「火焰丽鱼」比较难钓到：
```angex  
耐心【黄金幼虫|43849】@0802-1200（阴云|3=》小雨|7）》~9+17！！@专一【火焰丽鱼|43701】》！！！=【海牛灾星|49795】；不撒饵、鱼篓专一、引诱；  
pt [Golden Stonefly Nymph|43849] @0802 - 1200 (Clouds|3 => Rain|7) > ~ 9 + 17 !! @ic [Hunu Peacock Bass|43701] > !!! = [Punutiy Pain|49795]; nochum || sh-ic || lure;  
```  

## 11.3.15 等待专一
`等待专一` / `waitic` 表示在钓到应「专一垂钓」的目标时，如果采集力不足，则等待采集力。

在同时存在 `确保多提` / `mhs` 时，也应等待复数效果提钩技能所需的采集力，即先使用「专一垂钓」，然后等待复数效果提钩技能所需的采集力。

## 11.3.16 等待华丽
`等待华丽` / `wait-sh` 表示在「华丽提钩」进入冷却后，应等待，直到冷却时间小于目标的最短咬饵时间，再继续钓鱼。

## 11.3.17 套娃
`套娃` / `mooch-loop` 表示应使用为以小钓大循环优化的手法。

如：在耐心系状态效果即将结束时，再次使用耐心II刷新其时间，以防止以小钓大循环中断；如果在钓到可以小钓大的鱼时，确保钓上鱼类可以小钓大的效果已经结束，则使用熟练妙招将其保留为游动饵，然后再次换上以继续以小钓大循环。

## 11.3.18 引诱
`引诱` / `lure` 表示应按当前钓法使用「雄心之饵」或「谦逊之饵」直到最大层数或触发确保效果。

## 11.3.19 雄心
`雄心` / `a-lure` 表示应使用「雄心之饵」。单独出现时，是 `引诱` / `lure` 的显式表达，表示应使用直到最大层数或触发确保效果。

## 11.3.20 谦逊
`谦逊` / `m-lure` 表示应使用「谦逊之饵」。单独出现时，是 `引诱` / `lure` 的显式表达，表示应使用直到最大层数或触发确保效果。

## 11.3.21 鱼影
`鱼影` / `shadow` 表示应按当前钓法使用「雄心之饵」或「谦逊之饵」直到触发「鱼影出现提示语」。如果直到满层也未触发，则使用歇竿；如果同时存在 `引诱` / `lure` 则不使用歇竿。

## 11.3.22 重随
`重随` / `re-roll` 表示应在等待时间超过指定的最大咬饵时间时，使用「雄心之饵」或「谦逊之饵」来重新获取抛竿快照。在使用诱饵技能导致的咬饵真空期结束之后，如果仍未有咬饵，则再次使用。

## 11.3.23 回收
`回收` / `recy` 配合阶段参数中的复数效果提钩技能，表示应在采集力足够使用「大鱼猎手」与钓法中的复数效果提钩技能时，使用「大鱼猎手」以利用稍后的复数效果提钩技能来获取多层捕鱼人之计。

## 11.3.24 加钩
`加钩` / `mh` 表示应尽可能在合适时使用「加钩」。

- 合适的时机如：在钓法中不存在拍击水面或专一垂钓目标时，否则要求持有拍击水面或专一垂钓效果。

## 11.3.25 确保多提
`确保多提` / `mhs` 表示应保留更多的采集力以确保能使用钓法中采集力消耗最多的复数类型提钩技能。

## 11.3.26 多提回退
`多提回退` / `mhs-back` 表示在无法使用「三重提钩」时，应尝试「双重提钩」。

## 11.3.27 银星
`银星` / `silver` 表示应在满足银星需求时提前提交探索任务。

## 11.3.28 跳阶段
`跳阶段` / `skipstg` 表示计数器应以在当前钓场钓到的鱼类数量作为计数来源，而非本段钓法开始后钓到的鱼类数量。同时，使用复数效果提钩技能时也不再因为可能溢出而选择数量更少的复数效果提钩技能。（原来的钓法：假如「三重提钩」会导致计数器溢出，则使用数量更少的提钩技能）

## 11.3.29 强制预备
`强制预备` / `force-prep` 表示无论距离窗口期多久，都立刻开始窗口期前的准备工作。例如：捕鱼人之计、可以小钓大的鱼、游动饵、捕鱼人之识的触发等。



## 12. 阶段钓法
施工中……


# 附录

## 预设表格
在[预设表格](https://docs.qq.com/sheet/DVlNaQ012amF4SW1G?tab=yirja0)，你可以找到实际场景下的实用钓法表达式作为例子来参考。

## 测试条件
游戏进度：已可游玩 7.00 补丁的内容，以便测试  
捕鱼人：需要已经满级，且已经学习技能 耐心II

## 测试地点
**- 永夏岛北：**  
位置：拉诺西亚-东拉诺西亚，地图东北方的岛屿周围。  
在使用「沙蚕」作为钓饵时，永夏岛北有最稳定的咬饵。在抛竿后，咬饵的必定是「梅尔托尔虾虎」，在使用「梅尔托尔虾虎」来以小钓大后，咬饵的必定是「梭子鰆」。

**- 深壕：**  
位置：北洋地域-迷津，地图中央的围绕圆形地图块的沟渠。  
在用「以太螯虾」来以小钓大后，咬饵的仍可能是「以太螯虾」。可以测试「以小钓大循环」和捕鱼人之识。

**- 圣法斯里克天庭：**  
位置：诺弗兰特-伊尔美格，地图东南方的区域的河流。  
测试条件：有一把「诺弗兰特远见钓竿」（莫雯工具，物品 ID 41190），你需要装上前述的钓竿才能钓上测试所需的鱼类。  
在这里非常容易测试捕鱼人之识和捕鱼人之识结束时的确保咬饵效果。只需要钓上3条「丰饶虹鳉」就能触发对应「圣法斯里克的石头脑袋」的捕鱼人之识。